apply plugin: 'base'
allprojects {
	group = 'org.amse'
	version = '0.4-SNAPSHOT'    
}

subprojects {	
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'eclipse'
	
    repositories {
	    mavenCentral()
    }

    dependencies {
	    compile 'org.slf4j:slf4j-api:1.6.4'
	    compile 'org.slf4j:slf4j-log4j12:1.6.4'	
	    compile 'org.jboss.netty:netty:3.2.7.Final'  
	
	    testCompile 'junit:junit:4.6'
    }
}

task wrapper(type: Wrapper) {
	gradleVersion = '1.0'
}

ext.distDir = file("distribution")

evaluationDependsOn(":client")
task prepareClientDist(type: Zip) {
    description "Prepares client zip distributive"
    
    inputs.files project(":client").prepareDist
    
    destinationDir distDir
    archiveName "client.zip"

    from(project(":client").prepareDist) {
        exclude "log/**"
    }
    // no need to specify outputs. ZipTask uses archivePath as output!
}

evaluationDependsOn(":server")
task prepareServerDist(type: Zip) {
    description "Prepares server zip distributive"
    
    inputs.files project(":server").prepareDist
    
    destinationDir distDir
    archiveName "server.zip"
    
    from(project(":server").prepareDist) {
        exclude "log/**"
    }   
    // no need to specify outputs. ZipTask uses archivePath as output!
}

task prepareDist() {
    description "Prepares distributives"
    inputs.files prepareClientDist.outputs.files
    inputs.files prepareServerDist.outputs.files
    
    //TODO: add file with version and guide
    
    outputs.files inputs.files
}