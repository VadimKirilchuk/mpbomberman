apply plugin: 'base'
allprojects {
	group = 'org.amse'
	version = '0.4-SNAPSHOT'    
}

subprojects {	
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'eclipse'
	
    repositories {
	    mavenCentral()
    }

    dependencies {
	    compile 'org.slf4j:slf4j-api:1.6.4'
	    compile 'org.slf4j:slf4j-log4j12:1.6.4'	
	    compile 'org.jboss.netty:netty:3.2.7.Final'  
	
	    testCompile 'junit:junit:4.6'
    }
}

task wrapper(type: Wrapper) {
	gradleVersion = '1.0'
}

ext.distDir = file("distribution")
task prepareServerDist(type: Zip) {
    description "Prepares server distributive"
    
    inputs.files project(":server").jar
    inputs.file project(":server").file("resources")
    
    destinationDir distDir
    archiveName "server.zip"
    from(project(":server").jar) {
        rename { "bomberman-server.jar" }
    }
    from(project(":server").file("resources"))    
    // no need to specify outputs. ZipTask uses archivePath as output!
}

task prepareClientDist(type: Zip) {
    description "Prepares client distributive"
    
    inputs.files project(":client").jar
    inputs.file project(":client").file("resources")
    
    destinationDir distDir
    archiveName "client.zip"
    from(project(":client").jar) {
        rename { "bomberman.jar" }
    }
    from(project(":client").file("resources"))
    // no need to specify outputs. ZipTask uses archivePath as output!
}

task prepareDist() {
    description "Prepares distributives"
    inputs.files prepareClientDist.outputs.files
    inputs.files prepareServerDist.outputs.files
    
    //TODO: add file with version and guide
    
    outputs.files inputs.files
}